
defaultTasks 'clean', 'assemble', 'check', 'jacocoTestReport'

buildscript {

    repositories {

        jcenter()
        mavenCentral()
    }

    dependencies {

        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.3.1'
        classpath 'org.kt3k.gradle.plugin:coveralls-gradle-plugin:2.0.1'
    }
}

apply plugin: 'java'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'maven-publish'
apply plugin: 'jacoco'
apply plugin: 'com.github.kt3k.coveralls'

repositories {

    jcenter()
}

group = 'com.squidshoe'
version = '1.0.5'
jar.archiveName = "mfl-core.jar"

dependencies {

    compile (group: 'com.google.code.gson', name: 'gson', version: '2.3.1')
    compile (group: 'io.reactivex', name: 'rxjava', version: '1.0.8')
    compile (group: 'com.squareup.retrofit', name: 'retrofit', version: '1.9.0')
    compile (group: 'com.squareup.okhttp', name: 'okhttp', version: '2.2.0')
    compile (group: 'com.google.dagger', name: 'dagger', version: '2.0.1')
    compile (group: 'com.google.dagger', name: 'dagger-compiler', version: '2.0.1')

    testCompile (group: 'junit', name: 'junit', version: '4.12')
}

task sourceJar (type : Jar) {

    classifier = 'sources'
    from sourceSets.main.allSource
}

jacocoTestReport {
    reports {
        xml.enabled = true // coveralls plugin depends on xml format report
        html.enabled = true
    }
}

publishing {

    publications {

        mavenJava(MavenPublication) {
            from components.java
            groupId 'com.squidshoe'
            artifactId 'mfl-core'
            version project.version
        }

        mavenJavaSrc(MavenPublication) {
            artifact (sourceJar) {
                classifier = 'sources'
            }
        }
    }
}

def bintrayUsername = hasProperty('bintray_username') ? bintray_username : System.getenv('bintray_username')
def bintrayApiKey = hasProperty('bintray_api_key') ? bintray_api_key : System.getenv('bintray_api_key')
def bintrayRepo = hasProperty('bintray_repo') ? bintray_repo : System.getenv('bintray_repo')

bintray {

    user = bintrayUsername
    key = bintrayApiKey

    publications = ['mavenJava', 'mavenJavaSrc']

    dryRun = false
    publish = true

    pkg {

        repo = bintrayRepo
        name = 'mlf-core'
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/kkovach/mfl-core.git'
        publicDownloadNumbers = true

        version {

            name = project.version
            desc = "Core library for access to myfantasyleague.com's public API."
            released  = new Date()
            vcsTag = project.version
        }
    }
}

task wrapper(type: Wrapper) {

    gradleVersion = '2.6'
}
